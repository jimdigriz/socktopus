#!/usr/bin/env perl

use strict;
use warnings;

use v5.14;

use URI;
use AnyEvent;
use AnyEvent::Log;
use AnyEvent::Handle;
use AnyEvent::Socket;
use AnyEvent::Handle::UDP;
use IO::Socket::INET;
use Data::GUID qw(guid_string);
use Convert::ASN1 qw(:tag);
use Carp;
use POSIX qw(strftime);
use Socket qw(SOL_SOCKET SO_RCVBUF SO_SNDBUF);
use List::MoreUtils qw(uniq);

use Data::Dumper;

use constant SOCKTOPUS_OID	=> '2.25.97532385673460346314207847258541901083';

use constant RE_UDP		=> qr/^(?:\[?(?<host>[^\]]+?)\]?:)?(?:(?<serv>[a-z0-9-]+):)?(?:\[?(?<rhost>[^\]]+?)\]?:)?(?<rserv>[a-z0-9-]+)$/;
use constant RE_SERVICE		=> qr/^(?:\[?(?<host>[^\]]+?)\]?:)?(?<serv>[a-z0-9-]+)$/;

AnyEvent::Log::ctx->fmt_cb(sub { \@_ });
AnyEvent::Log::ctx->log_cb(sub {
	my $m = $AnyEvent::Log::LEVEL2STR[$_[0]->[2]] . ' ' . $_[0]->[3];
	$m = strftime('%F_%T.00000 ', gmtime($_[0]->[0])) . $m
		if (-t STDIN && -t STDOUT);

	if ($_[0]->[2] == 1) {
		Carp::cluck $m;
	} else {
		print STDERR "$m\n";
	}

	1;
});

open my $asn1, '<', 'socktopus.asn1'
	or AE::log fatal => "unable to open: $!";

my $asn = Convert::ASN1->new(tagdefault => 'EXPLICIT');
$asn->prepare($asn1)
	or AE::log fatal => 'ASN1 bust: ' . $asn->error;

seek $asn1, 0, 0;

my %oidfwd;
while (<$asn1>) {
	next unless /^--\s+socktopus([a-zA-Z0-9]+)\s+OBJECT IDENTIFIER\s+::=\s+{\s+socktopus\s+(\d+)\s+}$/;

	$oidfwd{$1} = SOCKTOPUS_OID . '.' . $2;

	my $h = $asn->find($1)
		or AE::log fatal => "'$1' handler not found";

	$asn->registeroid($oidfwd{$1}, $h);
}
my %oidrev = reverse %oidfwd;

close $asn1;

my $msg = $asn->find('Message')
	or AE::log fatal => "'Message' handler not found";

my @uri;
foreach my $a (@ARGV) {
	my $u = URI->new($a);

	AE::log fatal => "'$a' is not a supported URI endpoints"
		unless (defined($u) && $u->scheme ~~ [ 'tcp' ]);

	push @uri, $u;
}
my $mode = (scalar(@uri)) ? 'active' : 'passive';
AE::log info => "running $mode";

my (%stream, %map);
given ($mode) {
	when ('active') {
		AE::log warn => "first URI ('$uri[0]') should be TCP"
			unless ($uri[0]->scheme eq 'tcp');

		AE::log fatal => 'env UDPREM not set correctly ([host:][serv:][rhost:]rserv)'
			unless (defined($ENV{'UDPREM'}) && $ENV{'UDPREM'} =~ RE_UDP);

		AE::log fatal => 'env UDPLOC not set correctly ([host:][serv:][rhost:]rserv)'
			unless (defined($ENV{'UDPLOC'}) && $ENV{'UDPLOC'} =~ RE_UDP);

		my $host = $+{host} || 'localhost';
		my $serv = $+{serv} || '23461';
		my $rhost = $+{rhost} || 'localhost';
		my $fh = IO::Socket::INET->new(
			LocalHost	=> $host,
			PeerHost	=> $rhost,
			Proto		=> 'udp',
			LocalPort	=> $serv,
			PeerPort	=> $+{rserv},
			Blocking	=> 0,
		) or AE::log fatal => "bind: $@";

		my $handle = AnyEvent::Handle::UDP->new(
			fh		=> $fh,
			on_error	=> sub {
				AE::log $_[1] ? 'fatal' : 'error' => $_[2];
			},
			on_recv		=> \&forward,
		);

		$map{$fh->fileno} = {
			handle	=> $handle,
			tuple	=> join(' ', $host, $serv, $rhost, $+{rserv}),
			rhost	=> $fh->peerhost,
			rserv	=> $fh->peerport,
			primary	=> -1,
			feed	=> [ ],
			order	=> [ ],
		};

		# disconnect to avoid fatal 'connection refused'
		$fh->connect(pack('S n L x8', AF_UNSPEC, 0, 0));
	}
	when ('passive') {
		my $serv = $ENV{'SERVICE'} || '[::]:23461';
		AE::log fatal => 'env SERVICE not set correctly ([host:]serv)'
			unless ($serv =~ RE_SERVICE);

		tcp_server($+{host} || '::', $+{serv}, sub {
			my ($fh, $host, $port) = @_;

			# TODO AnyEvent::Socket->tcp_congestion

			setsockopt($fh, SOL_SOCKET, SO_RCVBUF, 5*1024)
				or AE::log warn => "unable to set SO_RCVBUF=5kB: $!";
			setsockopt($fh, SOL_SOCKET, SO_SNDBUF, 5*1024)
				or AE::log warn => "unable to set SO_SNDBUF=5kB: $!";

			my $handle = AnyEvent::Handle->new(fh => $fh);
			$stream{$fh->fileno} = {
				handle	=> $handle,
				state	=> 'ingress',
			};

			$handle->on_error(sub {
				AE::log $_[1] ? 'fatal' : 'error' => 'stream: ' . $_[2];
				&streamclose($_[0]);
			});
			$handle->on_read(\&read_rbuf);
			$handle->on_eof(sub {
				AE::log error => 'EOF, closing';
				&streamclose($_[0]);
			});
		});
	}
}

AnyEvent->condvar->recv;

exit 0;

sub read_rbuf {
	my ($handle) = @_;

	my ($tb,$tag) = asn_decode_tag($handle->{rbuf})
		or return;
	my ($lb,$len) = asn_decode_length(substr($handle->{rbuf}, $tb, 8))
		or return;
	return if (length($handle->{rbuf}) < $tb + $lb + $len);

	my $m = $msg->decode(substr $handle->{rbuf}, 0, $tb + $lb + $len);
	$handle->{rbuf} = substr $handle->{rbuf}, $tb + $lb + $len;

	recvmsg($handle, $m);

	1;
}

sub sendmsg {
	my ($handle, $type, %payload) = @_;

	AE::log fatal => "handler '$type' not found"
		unless (defined($oidfwd{$type}));

	my $m = $msg->encode(type => $oidfwd{$type}, content => \%payload);
	AE::log fatal => "unable to create '$type' Message"
		unless (defined($m));

	$handle->push_write($m);
}

sub recvmsg {
	my ($handle, $message) = @_;

	AE::log fatal => 'non-decodable message'
		unless (defined($message));

	my $t = $oidrev{$message->{type}};
	unless (defined($t)) {
		AE::log warn => 'unknown OID: ' . $message->{type};
		return;
	}

	if ($t eq 'Error') {
		AE::log error => 'received: ' . $message->{content}{message};
		return;
	}

	given ($stream{$handle->fh->fileno}{state}) {
		when ('ingress') {	# passive
			unless ($t eq 'Hello') {
				AE::log error => "expected Hello message, got $t";
				sendmsg($handle, 'Error', message => 'expected Hello message');
				&streamclose($handle);
				break;
			}

			unless ($message->{content}{version} == 0) {
				AE::log warn => "client requests unsupported version ${\$message->{content}{version}}";
				sendmsg($handle, 'Error', message => 'unsupported version');
				&streamclose($handle);
				break;
			}

			&streamconnect($handle, $message);
		}
		when ('egress') {	# active
			# TODO if invalid token error, then sleep and retry rather than reconnect
			unless ($t eq 'Connected') {
				AE::log error => "expected Connected message, got $t";
				sendmsg($handle, 'Error', message => 'expected Connected message');
				&streamclose($handle);
				break;
			}

			my $ufd = (keys %map)[0];

			if (defined($map{$ufd}{token})) {
				AE::log fatal => 'mismatch of token'
					unless ($map{$ufd}{token} eq $message->{content}{token});
			} else {
				$map{$ufd}{token} = $message->{content}{token};
			}

			push @{$map{$ufd}{feed}}, $handle->fh->fileno;

			if ($stream{$handle->fh->fileno}{primary} == 0) {
				unshift @{$map{$ufd}{order}}, $handle->fh->fileno;
			} else {
				unless ($map{$ufd}{primary} == -1) {
					AE::log error => 'primary already set';
					sendmsg($handle, 'Error', message => 'primary already set');
					&streamclose($handle);
					return;
				}

				$map{$ufd}{primary} = $handle->fh->fileno;
			}

			$stream{$handle->fh->fileno}{state} = 'member';
		}
		when ('member') {
			unless ($t eq 'Data') {
				AE::log error => "expected Data message, got $t";
				sendmsg($handle, 'Error', message => 'expected Data message');
				break;
			}

			my $ufd = (grep { $handle->fh->fileno ~~ [ @{$map{$_}{feed}} ] } keys %map)[0];
			unless (defined($ufd)) {
				AE::log error => 'orphaned fh';
				sendmsg($handle, 'Error', message => 'orphaned fh');
				&streamclose($handle);
				break;
			}

			$map{$ufd}{handle}->push_send($message->{content}{payload}, [ $map{$ufd}{rhost}, $map{$ufd}{rserv} ]);
		}
		default {
			AE::log fatal => "unexpected $t state";
		}
	}
}

sub forward {
	my ($data, $handle, $src) = @_;

	# TODO check from expected source or use BPF

	my $ufd = $handle->fh->fileno;
	my @order = grep { $stream{$_}{state} eq 'member' } @{$map{$ufd}{order}};
	unshift @order, $map{$ufd}{primary}
		unless ($map{$ufd}{primary} == -1);
	foreach my $fd (@order) {
		# TODO reduce buffer underuns (can we just test the length of wbuf?)
		vec(my $win = '', $stream{$fd}{handle}->fh->fileno, 1) = 1;
		if (select(undef, $win, undef, 0)) {
			# TODO handle sequence numbers to get jitter/rtt stats
			sendmsg($stream{$fd}{handle}, 'Data', sequence => { global => -1, local => -1 }, payload => $data);
			last;
		}

		next if ($fd == $map{$ufd}{primary});

		@{$map{$ufd}{order}} = grep { $_ != $fd } @{$map{$ufd}{order}};
		splice @{$map{$ufd}{order}}, int(rand(length(@{$map{$ufd}{order}}))), 0, $fd;
	}

	return unless ($mode eq 'active' && keys %stream < @uri);

	&streamopen($ufd, $_)
		foreach (uniq(@uri, map { $stream{$_}{uri} } keys %stream));
}

sub streamopen {
	my ($ufd, $luri) = @_;

	my ($h, $s) = split /:([^:]+)$/, $luri->authority, 2;
	$s = '23461'
		unless (defined($s));

	tcp_connect($h, $s, sub {
		my ($fh) = @_;

		unless (defined($fh)) {
			AE::log warn => "unable to connect: $!";
			return;
		}

		# TODO AnyEvent::Socket->tcp_congestion

		setsockopt($fh, SOL_SOCKET, SO_RCVBUF, 5*1024)
			or AE::log warn => "unable to set SO_RCVBUF=5kB: $!";
		setsockopt($fh, SOL_SOCKET, SO_SNDBUF, 5*1024)
			or AE::log warn => "unable to set SO_SNDBUF=5kB: $!";

		my $handle = AnyEvent::Handle->new(fh => $fh);
		$stream{$fh->fileno} = {
			handle	=> $handle,
			state	=> 'egress',
			primary	=> ($luri eq $uri[0]),
		};

		$handle->on_error(sub {
			AE::log $_[1] ? 'fatal' : 'error' => 'stream: ' . $_[2];
			&streamclose($_[0]);
		});
		$handle->on_read(\&read_rbuf);
		$handle->on_eof(sub {
			AE::log error => 'EOF, closing';
			&streamclose($_[0]);
		});

		$ENV{'UDPREM'} =~ RE_UDP;

		my %payload = (
			version		=> 0,
			primary		=> ($luri eq $uri[0]),
			host		=> $+{host} || 'localhost',
			service		=> $+{serv} || '23461',
			rhost		=> $+{rhost} || 'localhost',
			rservice	=> $+{rserv},
		);

		$payload{token} = $map{$ufd}{token}
			if (defined($map{$ufd}{token}));

		sendmsg($handle, 'Hello', %payload);
	}, sub {
		my ($fh) = @_;

		5;
	});
}

sub streamconnect {
	my ($handle, $message) = @_;

	my $tuple = join ' ', $message->{content}{host}, $message->{content}{service},
				$message->{content}{rhost}, $message->{content}{rservice};
	my $ufd = (grep { $map{$_}{tuple} eq $tuple } keys %map)[0];
	if (defined($ufd)) {
		unless (defined($message->{content}{token}) && $map{$ufd}{token} eq $message->{content}{token}) {
			AE::log note => 'missing or invalid token';
			sendmsg($handle, 'Error', message => 'requires valid token');
			return;
		}

		push @{$map{$ufd}{feed}}, $handle->fh->fileno;

		if ($message->{content}{primary} == 0) {
			unshift @{$map{$ufd}{order}}, $handle->fh->fileno;
		} else {
			unless ($map{$ufd}{primary} == -1) {
				AE::log error => 'primary already set';
				sendmsg($handle, 'Error', message => 'primary already set');
				&streamclose($handle);
				return;
			}

			$map{$ufd}{primary} = $handle->fh->fileno;
		}
	} else {
		my $fh = IO::Socket::INET->new(
			LocalHost	=> $message->{content}{host},
			PeerHost	=> $message->{content}{rhost},
			Proto		=> 'udp',
			LocalPort	=> $message->{content}{service},
			PeerPort	=> $message->{content}{rservice},
			Blocking	=> 0,
		);
		unless (defined($fh)) {
			AE::log warn => "bind: $@";
			sendmsg($handle, 'Error', message => "bind: $@");
			&streamclose($handle);
			return;
		};

		my $uhandle = AnyEvent::Handle::UDP->new(
			fh		=> $fh,
			on_error	=> sub {
				AE::log $_[1] ? 'fatal' : 'error' => $_[2];
			},
			on_recv		=> \&forward,
		);

		$ufd = $fh->fileno;

		$map{$ufd} = {
			handle	=> $uhandle,
			tuple	=> $tuple,
			rhost	=> $fh->peerhost,
			rserv	=> $fh->peerport,
			token	=> guid_string,
			primary	=> -1,
			feed	=> [ $handle->fh->fileno ],
			order	=> [ ],
		};

		if ($message->{content}{primary} == 0) {
			unshift @{$map{$ufd}{order}}, $handle->fh->fileno;
		} else {
			$map{$ufd}{primary} = $handle->fh->fileno;
		}

		$fh->connect(pack('S n L x8', AF_UNSPEC, 0, 0));
	}

	sendmsg($handle, 'Connected', token => $map{$ufd}{token});

	$stream{$handle->fh->fileno}{state} = 'member';
}

sub streamclose {
	my ($handle) = @_;

	$handle->on_drain(sub {
		delete $stream{$_[0]->fh->fileno};
		undef $_[0];
	});

	my $sfd = $handle->fh->fileno;

	my $ufd = (grep { $sfd ~~ [ @{$map{$_}{feed}} ] } keys %map)[0];
	return unless (defined($ufd));

	@{$map{$ufd}{feed}} = grep { $_ != $sfd } @{$map{$ufd}{feed}};

	@{$map{$ufd}{order}} = grep { $_ != $sfd } @{$map{$ufd}{order}};
	$map{$ufd}{primary} = -1
		if ($map{$ufd}{primary} == $sfd);

	return if (@{$map{$ufd}{feed}});

	AE::log note => 'no longer using UDP socket, closing';

	$map{$ufd}{handle}->destroy;
	delete $map{$ufd};
}
